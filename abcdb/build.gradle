import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

buildscript {
    dependencies {
        classpath("com.github.docker-java:docker-java-transport-httpclient5:3.2.13") {
            because("M1 macs need a later version of JNA")
        }
    }
}
plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.2"
    id("com.google.cloud.tools.jib") version "2.8.0"
}

version = "1.0"
group = "io.micronaut.examples"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.dekorate:kubernetes-annotations")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    implementation("io.dekorate:kubernetes-annotations")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.kubernetes:micronaut-kubernetes-discovery-client")
    implementation("jakarta.annotation:jakarta.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut:micronaut-validation")

}


application {
    mainClass.set("io.micronaut.examples.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

tasks {
    jib {
        to {
            image = "gcr.io/myapp/jib-image"
        }
    }
}
graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("io.micronaut.examples.*")
    }
}

tasks.withType(DockerBuildImage) {
    images = ["us-phoenix-1.ocir.io/oraclelabs/abcdb:$project.version"]
}

tasks.withType(Dockerfile) {
    baseImage = 'openjdk:17-slim'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'io.dekorate') {
            details.useVersion "3.0.0"
        }
    }
}


